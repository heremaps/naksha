import org.jetbrains.kotlin.gradle.targets.js.yarn.YarnPlugin

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.9.21'
}

kotlin {
    jvm {
        jvmToolchain(11)
        withJava()
        targetCompatibility = JavaVersion.VERSION_11
    }
    js(IR) {
        moduleName = "jbon"
        browser {
            webpackTask {
                output.libraryTarget = "commonjs2"
            }
        }
        nodejs {
        }
        generateTypeScriptDefinitions()
        //binaries.library()
        binaries.executable()
    }

    jvmProcessResources.dependsOn(jsBrowserProductionWebpack)
    jvmTestProcessResources.dependsOn(jsBrowserProductionWebpack, jsBrowserDistribution)
    jvmJar.dependsOn(jsBrowserProductionWebpack)

    sourceSets {
        commonMain {
            dependencies {
                implementation(kotlin("stdlib-common"))
            }
        }
        commonTest {
            dependencies {
                implementation(kotlin("test-common"))
                implementation(kotlin('test-annotations-common'))
            }
        }
        jvmMain {
            dependencies {
                implementation(kotlin("stdlib-jdk8"))
                implementation(project(":here-naksha-lib-core"))
                api("org.lz4:lz4-java:1.8.0")
            }
            resources.srcDirs += "$buildDir/dist/js/productionExecutable/"
        }
        jvmTest {
            dependencies {
                implementation(kotlin('test'))
                implementation('io.kotlintest:kotlintest-runner-junit5:3.3.2')
                runtimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")
                implementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
                implementation("org.junit.jupiter:junit-jupiter-params:5.5.2")
            }
            resources.srcDirs += "$buildDir/dist/js/productionExecutable/"
        }
        jsMain {
            dependencies {
                implementation(kotlin("stdlib-js"))
            }
        }
    }
}

tasks.jvmTest {
    useJUnitPlatform()
}